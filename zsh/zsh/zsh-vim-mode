# bindkey -e will be emacs mode
bindkey -v
bindkey -a -r ':'
export KEYTIMEOUT=1

# Use vim keys in tab complete menu:
bindkey -M menuselect '^h' vi-backward-char
bindkey -M menuselect '^k' vi-up-line-or-history
bindkey -M menuselect '^l' vi-forward-char
bindkey -M menuselect '^j' vi-down-line-or-history
bindkey -M menuselect '^[[Z' vi-up-line-or-history
bindkey -v '^?' backward-delete-char

# yank to system clipboard
function vi-yank-xclip {
    zle vi-yank
   echo "$CUTBUFFER" | clip.exe
}


# Change cursor shape for different vi modes.
function zle-keymap-select () {
    case $KEYMAP in
        vicmd) echo -ne '\e[4 q';;      # underline
        viins|main) echo -ne '\e[2 q';; # block
    esac
}

zle-line-init() {
    zle -K viins # initiate `vi insert` as keymap (can be removed if `bindkey -V` has been set elsewhere)
    echo -ne "\e[2 q"
}


zle -N zle-keymap-select
zle -N vi-yank-xclip
zle -N zle-line-init


# bind yank key
bindkey -M vicmd 'y' vi-yank-xclip

echo -ne '\e[2 q' # Use block shape cursor on startup.

preexec() { echo -ne '\e[2 q' ;} # Use block shape cursor for each new prompt.
